load(":optional_dependency_groups.bzl", "OPTIONAL_DEPENDENCY_GROUPS")

package(default_visibility = ["//visibility:public"])

exports_files(["optional_dependency_groups.bzl"])

constraint_setting(name = "core_env")

[
    constraint_setting(
        name = group_name + "_env",
    )
    for group_name in OPTIONAL_DEPENDENCY_GROUPS.keys()
]

constraint_value(
    name = "core_conda_channel",
    constraint_setting = ":core_env",
)

[
    constraint_value(
        name = group_name + "_conda_channel",
        constraint_setting = ":" + group_name + "_env",
    )
    for group_name in OPTIONAL_DEPENDENCY_GROUPS.keys()
]

platform(
    name = "core_conda_env",
    constraint_values = [
        ":core_conda_channel",
    ],
    parents = ["@local_config_platform//:host"],
)

[
    platform(
        name = group_name + "_conda_env",
        constraint_values = [
            ":core_conda_channel",
            ":" + group_name + "_conda_channel",
        ],
        parents = ["@local_config_platform//:host"],
    )
    for group_name in OPTIONAL_DEPENDENCY_GROUPS.keys()
]

platform(
    name = "all_conda_env",
    constraint_values = [
        ":core_conda_channel",
    ] + [
        ":" + group_name + "_conda_channel"
        for group_name in OPTIONAL_DEPENDENCY_GROUPS.keys()
    ],
    parents = ["@local_config_platform//:host"],
)
